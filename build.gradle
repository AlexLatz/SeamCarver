plugins {
    id 'application'
    id 'idea'
    id 'org.openjfx.javafxplugin' version "0.0.9"
    id 'org.beryx.jlink' version '2.21.3'
}

group 'ga.alexlatz'
version '0.1.0'

mainClassName = "ga.alexlatz.seamcarverapp.SeamCarverWindow"

repositories {
    mavenCentral()
}
javafx {
    version = "11"
    modules = ['javafx.controls', 'javafx.swing']
}
jlink {
    launcher {
        name = 'SeamCarver'
    }
    jpackage {
        installerOptions = [
                '--description', 'SeamCarver is a application that uses the Seam Carving algorithm to resize images with content-aware scaling.',
                '--copyright', 'Â© 2020 Alex Latz'
        ]
        installerType = project.findProperty('installerType') // we will pass this from the command line (example: -PinstallerType=msi)
        if (installerType == 'msi') {
            imageOptions += ['--icon', 'src/main/resources/icon.ico']
            installerOptions += [
                    '--win-per-user-install', '--win-dir-chooser',
                    '--win-menu'
            ]
        }
        if (installerType == 'dmg') {
            imageOptions += ['--icon', 'src/main/resources/icon.icns']
        }
        if (installerType in ['deb', 'rpm']) {
            imageOptions += ['--icon', 'src/main/resources/icon.png']
            installerOptions += [
                    '--linux-menu-group', 'Office',
                    '--linux-shortcut'
            ]
        }
        if (installerType == 'deb') {
            installerOptions += [
                    '--linux-deb-maintainer', 'alexlatz@pm.me'
            ]
        }
    }
}
jpackage {
    doFirst {
        project.getProperty('installerType')
    }
}
jar {
    manifest {
        attributes 'Main-Class':'ga.alexlatz.seamcarverapp.SeamCarverWindow'
    }
}
